from random import random_float64
from mmm_audio import *

struct WhiteNoise[num_chans: Int = 1](Copyable, Movable):
    """Generate white noise samples.
    
    Parameters:
        num_chans: Number of SIMD channels.
    """
    fn __init__(out self):
        """Initialize the WhiteNoise struct."""
        pass  # No initialization needed for white noise

    fn next(self, gain: SIMD[DType.float64, Self.num_chans] = SIMD[DType.float64, Self.num_chans](1.0)) -> SIMD[DType.float64, Self.num_chans]:
        """Generate the next white noise sample.

        Args:
            gain: Amplitude scaling factor.
        
        Returns:
            A random value between -gain and gain.
        """
        # Generate random value between -1 and 1, then scale by gain
        return random_uni_float64[Self.num_chans](-1.0, 1.0) * gain

struct PinkNoise[num_chans: Int = 1](Copyable, Movable):
    """Generate pink noise samples.

    Uses the [Voss-McCartney algorithm](https://www.firstpr.com.au/dsp/pink-noise/#Voss-McCartney).

    Parameters:
        num_chans: Number of SIMD channels.
    """

    var b0: SIMD[DType.float64, Self.num_chans]
    var b1: SIMD[DType.float64, Self.num_chans]
    var b2: SIMD[DType.float64, Self.num_chans]
    var b3: SIMD[DType.float64, Self.num_chans]
    var b4: SIMD[DType.float64, Self.num_chans]
    var b5: SIMD[DType.float64, Self.num_chans]
    var b6: SIMD[DType.float64, Self.num_chans]

    fn __init__(out self):
        """Initialize the PinkNoise struct."""
        self.b0 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b1 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b2 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b3 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b4 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b5 = SIMD[DType.float64, Self.num_chans](0.0)
        self.b6 = SIMD[DType.float64, Self.num_chans](0.0)

    fn next(mut self, gain: SIMD[DType.float64, Self.num_chans] = SIMD[DType.float64, Self.num_chans](1.0)) -> SIMD[DType.float64, Self.num_chans]:
        """Generate the next pink noise sample.

        Args:
            gain: Amplitude scaling factor.

        Returns:
            The next pink noise sample scaled by gain.
        """
        # Generate white noise SIMD
        var white = random_uni_float64[Self.num_chans](-1.0, 1.0)

        # Filter white noise to get pink noise (Voss-McCartney algorithm)
        self.b0 = self.b0 * 0.99886 + white * 0.0555179
        self.b1 = self.b1 * 0.99332 + white * 0.0750759
        self.b2 = self.b2 * 0.96900 + white * 0.1538520
        self.b3 = self.b3 * 0.86650 + white * 0.3104856
        self.b4 = self.b4 * 0.55000 + white * 0.5329522
        self.b5 = self.b5 * -0.7616 - white * 0.0168980

        # Sum the filtered noise sources
        var pink = self.b0 + self.b1 + self.b2 + self.b3 + self.b4 + self.b5 + self.b6 + white * 0.5362

        # Scale and return the result
        return pink * (gain * 0.125)

struct BrownNoise[num_chans: Int = 1](Copyable, Movable):
    """Generate brown noise samples.

    Parameters:
        num_chans: Number of SIMD channels.
    """

    var last_output: SIMD[DType.float64, Self.num_chans]

    fn __init__(out self):
        """Initialize the BrownNoise struct."""
        self.last_output = SIMD[DType.float64, Self.num_chans](0.0)

    fn next(mut self, gain: SIMD[DType.float64, Self.num_chans] = SIMD[DType.float64, Self.num_chans](1.0)) -> SIMD[DType.float64, Self.num_chans]:
        """Generate the next brown noise sample.

        Args:
            gain: Amplitude scaling factor.

        Returns:
            The next brown noise sample scaled by gain.
        """
        # Generate white noise SIMD
        var white = random_uni_float64[Self.num_chans](-1.0, 1.0)

        # Integrate white noise to get brown noise
        self.last_output += (white - self.last_output) * 0.02
        return self.last_output * gain

struct TExpRand[num_chans: Int = 1](Copyable, Movable):
    """Generate exponentially distributed random value upon receiving a trigger.

    Parameters:
        num_chans: Number of SIMD channels.
    """

    var stored_output: SIMD[DType.float64, Self.num_chans]
    var last_trig: SIMD[DType.bool, Self.num_chans]
    var is_initialized: Bool

    fn __init__(out self):
        """Initialize the TExpRand struct."""
        self.stored_output = SIMD[DType.float64, Self.num_chans](0.0)
        self.last_trig = SIMD[DType.bool, Self.num_chans](fill=False)
        self.is_initialized = False

    fn next(mut self, min: SIMD[DType.float64, Self.num_chans], max: SIMD[DType.float64, Self.num_chans], trig: SIMD[DType.bool, Self.num_chans]) -> SIMD[DType.float64, Self.num_chans]:
        """Output the exponentially distributed random value.

        The value is repeated until a new trigger is received, at which point a new value is generated.
        And that new value is repeated until the next trigger, and so on.
        
        Args:
            min: Minimum value for the random value.
            max: Maximum value for the random value.
            trig: Trigger to generate a new value.

        Returns:
            The exponentially distributed random value.
        """
        
        if not self.is_initialized: 
            @parameter
            for i in range(Self.num_chans):
                self.stored_output[i] = random_exp_float64(min[i], max[i])
            self.is_initialized = True
            return self.stored_output
        
        rising_edge: SIMD[DType.bool, Self.num_chans] = trig & ~self.last_trig
        @parameter
        for i in range(Self.num_chans):
            if rising_edge[i]:
                self.stored_output[i] = random_exp_float64(min[i], max[i])
        self.last_trig = trig
        return self.stored_output

struct TRand[num_chans: Int = 1](Copyable, Movable):
     """Generate uniformly distributed random value upon receiving a trigger.

    Parameters:
        num_chans: Number of SIMD channels.
    """

    var stored_output: SIMD[DType.float64, Self.num_chans]
    var last_trig: SIMD[DType.bool, Self.num_chans]
    var is_initialized: Bool

    fn __init__(out self):
        """Initialize the TRand struct."""
        self.stored_output = SIMD[DType.float64, Self.num_chans](0.0)
        self.last_trig = SIMD[DType.bool, Self.num_chans](fill=False)
        self.is_initialized = False

    fn next(mut self, min: SIMD[DType.float64, Self.num_chans], max: SIMD[DType.float64, Self.num_chans], trig: SIMD[DType.bool, Self.num_chans]) -> SIMD[DType.float64, Self.num_chans]:
        """Output uniformly distributed random value.

        The value is repeated until a new trigger is received, at which point a new value is generated.
        And that new value is repeated until the next trigger, and so on.

        Args:
            min: Minimum value for the random value.
            max: Maximum value for the random value.
            trig: Trigger to generate a new value.

        Returns:
            The uniformly distributed random value.
        """

        if not self.is_initialized: 
            @parameter
            for i in range(Self.num_chans):
                self.stored_output[i] = random_float64(min[i], max[i])
            self.is_initialized = True
            return self.stored_output

        rising_edge: SIMD[DType.bool, Self.num_chans] = trig & ~self.last_trig
        @parameter
        for i in range(Self.num_chans):
            if rising_edge[i]:
                self.stored_output[i] = random_float64(min[i], max[i])
        self.last_trig = trig
        return self.stored_output

struct LFSRNoise[num_chans: Int = 1](Copyable, Movable):
    """Generate noise using a Linear Feedback Shift Register (LFSR).

    Based on [Josiah Sytsma's LFSR implementation](https://www.mjsyts.com/development/lfsr-noise-part-3).
    
    Parameters:
        num_chans: Number of SIMD channels.
    """

    var state:                  SIMD[DType.uint32, Self.num_chans]
    var width:                  SIMD[DType.uint32, Self.num_chans]
    var mask:                   SIMD[DType.uint32, Self.num_chans]
    var phase:                  SIMD[DType.float64, Self.num_chans]
    var freq_mul:               Float64
    var rising_bool_detector:   RisingBoolDetector[Self.num_chans]
    var world:                  World

    fn __init__(out self, world: World, width: SIMD[DType.uint32, Self.num_chans]):
        self.world                = world
        self.freq_mul             = 1.0 / self.world[].sample_rate
        self.state                = SIMD[DType.uint32, Self.num_chans](1)
        self.phase                = SIMD[DType.float64, Self.num_chans](0.0)
        self.width                = SIMD[DType.uint32, Self.num_chans](0)
        self.mask                 = SIMD[DType.uint32, Self.num_chans](0)
        self.rising_bool_detector = RisingBoolDetector[Self.num_chans]()
        self.set_width(width)

    fn set_width(mut self, width: SIMD[DType.uint32, Self.num_chans]):
        """Set the width of the LFSR."""
        self.width = clip(width, SIMD[DType.uint32, Self.num_chans](3), SIMD[DType.uint32, Self.num_chans](32))
        self.mask = (self.width.eq(32)).select(
            SIMD[DType.uint32, Self.num_chans](0xFFFFFFFF),
            (SIMD[DType.uint32, Self.num_chans](1) << self.width) - 1
        )

    fn reset(mut self):
        """Reset the LFSR state to 1."""
        self.state = SIMD[DType.uint32, Self.num_chans](1)
        self.phase = SIMD[DType.float64, Self.num_chans](0.0)

    @doc_private
    @always_inline
    fn step(mut self):
        self.state = self.state.eq(0).select(SIMD[DType.uint32, Self.num_chans](1), self.state)
        var lsb0 = self.state & 1
        var lsb1 = (self.state >> 1) & 1
        var fb   = lsb0 ^ lsb1
        self.state = (self.state >> 1) | (fb << (self.width - 1))
        self.state &= self.mask

    @always_inline
    fn next(mut self, freq: SIMD[DType.float64, Self.num_chans] = 1.0, gain: SIMD[DType.float64, Self.num_chans] = 1.0, trig: Bool = False) -> SIMD[DType.float64, Self.num_chans]:
        """Generate the next LFSR noise sample.

        Args:
            freq: Frequency at which to step the LFSR in Hz.
            gain: Amplitude scaling factor.
            trig: Trigger signal to reset LFSR state.

        Returns:
            The next LFSR noise sample scaled by gain.
        """
        var trig_mask = SIMD[DType.bool, Self.num_chans](fill=trig)
        var resets = self.rising_bool_detector.next(trig_mask)
        var clamped_freq = clip(freq, SIMD[DType.float64, Self.num_chans](0.0), SIMD[DType.float64, Self.num_chans](self.world[].sample_rate))
        var incremented_phase = self.phase + (clamped_freq * self.freq_mul)
        var wrapped: SIMD[DType.bool, Self.num_chans] = (incremented_phase >= 1.0)
        var old_state = self.state
        self.step()
        self.state = wrapped.select(self.state, old_state)
        self.phase = wrapped.select(incremented_phase - 1.0, incremented_phase)
        self.state = resets.select(SIMD[DType.uint32, Self.num_chans](1), self.state)
        self.phase = resets.select(SIMD[DType.float64, Self.num_chans](0.0), self.phase)
        var out = (self.state & 1).cast[DType.float64]() * 2.0 - 1.0
        return out * gain